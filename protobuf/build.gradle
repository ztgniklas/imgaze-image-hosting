plugins {
    id 'java-library'
    id 'com.google.protobuf' version '0.9.2'
}

group 'com.taigezhang.grpc_protobuf.protobuf'
version '0.0.1'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'javax.annotation:javax.annotation-api:1.3.2'

    implementation "io.grpc:grpc-all:$grpc_version"
    implementation "io.grpc:grpc-kotlin-stub:$grpc_kotlin_version"
    implementation "com.google.protobuf:protobuf-java:$protobuf_version"
    implementation "com.google.protobuf:protobuf-kotlin:$protobuf_version"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

test {
    useJUnitPlatform()
}

sourceSets {
    main {
        proto {
            srcDirs 'src/main/proto'
        }
    }
}

processResources {
    exclude '**/*.proto'
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }

    plugins {
        grpc {
            if (osdetector.os == "osx") {
                artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version:osx-x86_64"
            } else {
                artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version"
            }
        }
        grpckt {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpc_kotlin_version:jdk8@jar"
        }
    }

    generateProtoTasks {
        all().forEach { task ->
            task.plugins {
                grpc {}

                // coroutines {}

                grpckt {}
            }
        }
    }

    generatedFilesBaseDir = "$projectDir/generated"
}